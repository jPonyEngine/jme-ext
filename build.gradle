plugins {
    id 'java'
    id 'maven'
    id 'signing'
}

group 'io.github.jayfella'
archivesBaseName = "jme-ext"
version '1.0.0'

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier.set("sources")
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier.set("javadoc")
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

repositories {
    mavenCentral()
}

project.ext {
    jme_ver = "3.3.2-stable"
}

dependencies {
    implementation "org.jmonkeyengine:jme3-core:$jme_ver"
    implementation "org.jmonkeyengine:jme3-desktop:$jme_ver"
    implementation "org.jmonkeyengine:jme3-lwjgl:$jme_ver"

    implementation "org.jmonkeyengine:jme3-plugins:$jme_ver"
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'jme-ext'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'An extension of the JME Application'
                url 'https://github.com/jayfella/jme-ext'

                scm {
                    connection 'https://github.com/jayfella/jme-ext'
                    developerConnection 'https://github.com/jayfella/jme-ext.git'
                    url 'https://github.com/jayfella/jme-ext'
                }

                licenses {
                    license {
                        name 'The 3-Clause BSD License'
                        url 'https://opensource.org/licenses/BSD-3-Clause'
                    }
                }

                developers {
                    developer {
                        id 'jayfella'
                        name 'James Khan'
                        // email 'jayfella@gmail.com'
                    }
                }
            }
        }
    }
}